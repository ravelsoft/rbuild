{ readFile, writeFile, watchFile, stat } = require \fs
{ Bundler } = require './bundler'

function writeResult (path, data, cbk)
    (err) <- writeFile path, data, \utf-8
    if err
        console.log "#path: #err"
        return
    cbk?!

/**
 *  @class Task
 */
class Task

    /**
     *  @constructor
     */
    ({ @dependencies ? [], @callback, @watch } ) ->
        @watching = false

    /**
     *
     */
    run: function
        if @watch and not @watching
            @watching = true
            for f of @dependencies
                watchFile f, (curr, prev) ~>
                    if curr.mtime > prev.mtime
                        @callback!
        @callback!

/**
 *  @class TaskPool
 */
class TaskPool

    /**
     *  @constructor
     */
    ({ @watch }) ->
        @tasks = []
    
    /**
     *
     */
    createTaskForFile: function (file, outfile, callback)

        function fileCallback (error)
            (err_in, istat) <- stat file
            (err_out, ostat) <- stat outfile

            if not err_out and istat.mtime <= ostat.mtime
                # Not compiling to a file that is more recent.
                console.log "= #file"
                return

            readFile file, \utf-8, (err, data) ->
                if err
                    console.log "#file: #err"
                    return
                try
                    res = callback data
                    <- writeResult outfile, res
                    console.log "#{new Date!.toLocaleTimeString!}: #file => #outfile"
                catch e
                    console.log "#file: #e"

        return @addTask new Task do
            dependencies: [file]
            callback: fileCallback

    /**
     *
     */
    createBundleTask: function (outfile, files, extensions)
        bundler = new Bundler files: files, watch: @watch, extensions: extensions

        return @addTask new Task do
            dependencies: [] # The bundler does that.
            callback: ->
                (err, data) <- bundler.bundle!
                <- writeResult outfile, data
                console.log "* Wrote #outfile"

    /**
     *
     */
    addTask: function (task)
        @tasks.push task
        task.watch = @watch
        task.run!
        return task

exports <<< { Task, TaskPool }

