{ dirname, join, resolve, existsSync, basename } = require \path
{ readFile } = require \fs

/**
 *  @class BundleFile
 *
 *  Handles dependencies checking and its own compilation.
 */
class BundleFile
    last_id = 0
    cached = {}
    body_re = //\brequire\s*\(\s*(
                    "(\\\"|[^"])*"    |     # double quoted string
                    '(\\\'|[^'])*'          # single quoted
                )\s*\)//g

    @get = function (path, extensions)
        path = path.replace /\.[a-zA-Z_]+$/, ''

        return cached[path] if cached[path]

        return cached[path] = new BundleFile path, extensions

    (@path, @extensions) ->
        @compile_fn = null
        @error = false
        @body = null
        @deps = []
        @recompile = true

        @name = "#{basename @path}-#{last_id}"
        last_id := last_id + 1

    getRelativeName: function (path)
        return @path.replace (dirname path), '.'

    /**
     *  Compiles the module, and parse its contents, triggering the calling of 
     *
     *  @param cbk The callback for when we're done.
     *
     *  @returns An empty string if error, the body of the function otherwise.
     */
    getBody: function (cbk)
        if not @recompile
            cbk null, @body
            return

        @recompile = false

        if not @compile_fn?
            for ext, fn in @extensions
                realpath = "#{@path}.#ext"
                if existsSync realpath
                    @realpath = realpath
                    @compile_fn = fn
                    break

        if not @realpath
            cbk new Error "The module #{@path} either doesn't exist or doesn't have a valid module handler"
            return

        (err, body) <~ readFile @realpath, \utf-8
        if err
            @error = true
            cbk err, null
            return

        console.log "Compiling #{@realpath}"

        @body = @compile_fn body
        @error = false
        @deps = []

        @body = @body.replace body_re, (str) ~>
            mod_name = str.replace /.*require\s*\(\s*["']/, ''
                .replace /['"]\s*\)$/, ''

            dep_path = resolve (dirname @path), mod_name
            f = BundleFile.get dep_path, @extensions
            @deps.push f

            return "require('#{f.name}')"

        cbk null, @body


/**
 *  @class Bundler
 *
 *  A bundling class.
 */
class Bundler
    last_id = 0
    cached = {}

    /**
     *  @constructor
     */
    ({ extensions, files }) ->

        if typeof files is \string
            files = [files]

        files = (resolve "#{process.cwd!}/", f for f of files)

        @files = files
        @entry = @files.0

        @extensions =
            js: (str) -> str

        for ext, fn in extensions
            ((ext, fn) ~>
                @addExtension ext, (data, path) ->
                    try
                        fn data, path
                    catch e
                        console.log "!! #path: #e"
            )(ext, fn)

    /**
     *  Add an extension.
     *
     *  @param ext The extension
     *  @param cbk The callback that will handle its compilation.
     */
    addExtension: function (ext, cbk)
        @extensions[ext] = cbk

    /**
     *  Resolve a module's path from another.
     *
     *  First, compute the path difference between the two paths,
     *  then look for a file with all the registered extensions.
     *
     *  There is no promise on the order with which the extensions
     *  are looked for.
     *
     *  Once the file is found, it is read and tranformed according
     *  to its extension handler.
     *
     *  @param base The module where the resolving request's been
     *      made.
     *  @param path The path of the module.
     *  @param callback The callback to execute once we're done with
     *      the file. (error, contents)
     */
    processModules: function (path, callback)
        file = BundleFile.get path, @extensions

        (err) <~ file.getBody!

        if not file.error
            if file.deps.length is 0
                callback err

            loaded = 0
            for f of file.deps
                (err) <~ @processModules f.path
                loaded := loaded + 1
                if loaded is file.deps.length
                    callback err
            
    /**
     *
     */
    bundleModule: function (mod)
        return """
            __modules_fn['#{mod.name}'] = function (module) {
                var exports = module.exports;
                #{mod.body}
            };
            __modules['#{mod.name}'] = {};
        """

    /**
     *  Declaration of the general functions that are to be embedded into
     *  the resulting bundle.
     *
     *  @returns a string with the function declarations we're embedding.
     */
    toplevel: function

        /**
         *
         */
        function require (path)
            if not __modules[path].exports?
                module = __modules[path]
                module.exports = {}

                __modules_fn[path] module

            return __modules[path].exports

        res = """
            var __modules_fn = {};
            var __modules = {};

            #{require.toString!}
        """

        return res
    
    /**
     *  Call the callback with the bundle of all the files.
     */
    bundle: function (cbk)
        @error = false

        processed = 0

        for f of @files
            (err) <~ @processModules f
            if err
                console.log '!! ' + err
                return
            processed := processed + 1
            if processed is @files.length

                result = [@toplevel!]

                modules = {}
                @modules = modules

                get_modules = (f) ->
                    if f.name in modules # we already have it.
                        return

                    modules[f.name] = f
                    for m of f.deps
                        get_modules m

                for p of @files
                    get_modules BundleFile.get p

                for m, f in modules
                    if m.error
                        console.log "!! #{m.name} has errors, can't bundle"
                        cbk new Error "Error in one of the source"
                        return

                    result.push @bundleModule f
                
                first = BundleFile.get @files.0

                alias_list = []
                
                for m, f in modules
                    alias_list.push "__modules['#{f.getRelativeName first.path}'] = __modules['#{f.name}'];"
                    alias_list.push "__modules_fn['#{f.getRelativeName first.path}'] = __modules_fn['#{f.name}'];" 

                result.push alias_list.join "\n"
                result = result.join "\n"
                cbk null, result



exports import { Bundler, BundleFile }
